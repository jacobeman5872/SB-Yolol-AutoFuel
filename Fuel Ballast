F1=:Rod1 F2=:Rod2 F3=:Rod3 T1=F1>=F2 T2=F2>=F3 T3=F1>=F3 O1=T1*T3 
O2=(T1<1)*T2 O3=(T2<1)*(T3<1) Fr=O1*:FRate1+O2*:FRate2+O3*:FRate3
Fm=O1*F1+O2*F2+O3*F3 P1=F1/Fm P2=F2/Fm P3=F3/Fm
:FRL1=P1*Fr+O1*100 :FRL2=P2*Fr+O2*100 :FRL3=P3*Fr+O3*100 GOTO1

  // VARIABLES
// F1-3 (Fuel) = Fuel
// T1-3 (Test) = Test which fuel rods are more full
// O1-3 (Outcome) = Results of the test
// Fr (Fuel Rate) = Current fuel rate of the fullest fuel rod
// Fm (Fuel Max) = The most fuel in one of the rods
// P1-3 (Percent) = Percentage of fuel in rod compared to the max

  // INPUTS
// :Rod1-3 = Fuel rods' fill level// :FRate1-3 = Fuel rate of each chamber
// :FRate1-3 = Fuel rate of each chamber

  // OUTPUTS
// :FRL1-3 = Fuel rate limit for each chamber, or 100 for the max

  // FUNCTION
// Balance the load on each rod so they drain at the same rate
